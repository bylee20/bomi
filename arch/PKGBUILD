# Maintainer: xylosper <darklin20@gmail.com>
# Contributor: willemw <willemw12@gmail.com>

pkgname=bomi
pkgver=0.9.0
pkgrel=2
pkgdesc="Powerful and easy-to-use multimedia player"
arch=('i686' 'x86_64')
url="http://$pkgname-player.github.io"
license=('GPL')
provides=('cmplayer')
install=$pkgname.install
depends=('qt5-base' 'qt5-declarative' 'qt5-x11extras' 'qt5-quickcontrols' 'icu'
         'libdvdread' 'libdvdnav' 'libcdio-paranoia' 'libcdio'
         'alsa-lib' 'libpulse' 'jack' 'libchardet' 'libbluray'
         'mpg123' 'libva' 'libgl' 'fribidi' 'libass' 'ffmpeg')
makedepends=('mesa' 'gcc' 'pkg-config' 'python')
optdepends=('libaacs: AACS decryption for Blu-ray support'
            'libbdplus: BD+ decryption for Blu-ray support'
            'youtube-dl: streaming website support including YouTube')
source=($pkgname-$pkgver.tar.gz::https://github.com/xylosper/bomi/archive/v$pkgver.tar.gz)
md5sums=('67d252b27c6d3f771e9399b1c045f563')
options=(debug !strip)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-jack --enable-cdda --enable-pulseaudio
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DEST_DIR=$pkgdir install
}
