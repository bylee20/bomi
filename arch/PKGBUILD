# Maintainer: xylosper <darklin20@gmail.com>
# Contributor: willemw <willemw12@gmail.com>

pkgname=cmplayer
pkgver=0.8.10
pkgrel=1
pkgdesc="Powerful and easy-to-use multimedia player"
arch=('i686' 'x86_64')
url="http://$pkgname.github.io"
license=('GPL')
install=$pkgname.install
depends=('qt5-base' 'qt5-declarative' 'qt5-x11extras' 'qt5-quickcontrols' 'icu'
         'libdvdread' 'libdvdnav' 'libcdio-paranoia' 'libcdio' 'libquvi'
         'alsa-lib' 'libpulse' 'portaudio' 'jack' 'libchardet'
         'mpg123' 'libva' 'libgl' 'fribidi' 'libass' 'ffmpeg')
makedepends=('mesa' 'gcc' 'pkg-config' 'python')
source=(https://github.com/xylosper/cmplayer/releases/download/v$pkgver/cmplayer-$pkgver-source.tar.gz)
md5sums=('5848cb113f01bbf467a1978e77711bd6')
options=(debug !strip)

build() {
  export MAKE="make -j $(nproc)"
  cd "$srcdir/$pkgname-$pkgver"
  ./build-mpv
  make PREFIX=/usr LIBQUVI_SUFFIX=-0.9 cmplayer
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DEST_DIR=$pkgdir PREFIX=/usr LIBQUVI_SUFFIX=-0.9 install
}
